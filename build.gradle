version = '1.3.2'

allprojects {
	apply plugin: 'java-library'
	apply plugin: 'maven-publish'

	group = 'brownshome'

	java {
		withSourcesJar()
		withJavadocJar()

		sourceCompatibility = JavaVersion.VERSION_16
	}

	javadoc {
		options {
			source = java.sourceCompatibility.majorVersion
			links += "https://docs.oracle.com/en/java/javase/${java.sourceCompatibility.majorVersion}/docs/api".toString()
		}
	}

	repositories {
		mavenCentral()
	}

	publishing {
		repositories {
			maven {
				name = 'GitHubPackages'
				url = uri('https://maven.pkg.github.com/Brownshome/logging')
				credentials {
					username = project.properties['gpr.usr']
					password = project.properties['gpr.key']
				}
			}
		}
	}
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			artifactId = 'logging'
			from components.java

			pom {
				name = 'Logging'
				description = 'A simple logging library'
				url = 'https://github.com/Brownshome/logging'

				developers {
					developer {
						id = 'Brownshome'
						name = 'James Brown'
						email = 'jamesphone180@gmail.com'
					}
				}

				scm {
					connection = 'scm:git:git://github.com/Brownshome/logging.git'
					url = 'https://github.com/Brownshome/logging'
				}
			}
		}
	}
}